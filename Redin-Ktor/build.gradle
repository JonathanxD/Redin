buildscript {
    ext.kotlin_version = '1.4.32'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.github.jonathanxd'
version '1.1.3'

// Programming Language
apply plugin: 'kotlin'

// License
apply plugin: 'license'

// Shade
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation rootProject

    // Kotlin
    implementation 'io.ktor:ktor-server-core:1.5.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Utilities
    implementation 'com.github.koresframework.Kores:Kores:4.0.1.base'
    implementation 'com.github.koresframework:KoresProxy:2.6.1'
    implementation 'com.github.JonathanxD.JwIUtils:JwIUtils:4.17.2'
    implementation 'com.github.JonathanxD.JwIUtils:jwiutils-kt:4.17.2'
    implementation 'javax.inject:javax.inject:1'

    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': 'Redin-Ktor',
                'Implementation-Version': project.version
    }
}

shadowJar {
    classifier = 'shaded'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'JAVADOC_STYLE'
        kt = 'JAVADOC_STYLE'
    }
}
